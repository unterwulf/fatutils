#!/usr/bin/poke -L
!#
/* (c) 2021 Vitaly Sinilin.  */

load fat;

fun dump_info = (int32 fd) void:
{
    var br = unmap FAT_boot_record @ fd : 0#B;
    br._print;

    var nr_fat_clusters = br.bpb.sectors_per_fat as uint64 * br.bpb.bytes_per_sector * 8 / br.bpb.fat_bits;
    printf "\n       Clusters in FAT: %u32d\n", nr_fat_clusters;
    printf "         Data clusters: %u32d\n\n", br.bpb.nr_data_clusters;
}

if (argv'length != 1) {
    print "usage: fatinfo IMAGE\n";
    exit(1);
}

var filename = argv[0];

try {
    var fd = open(filename, IOS_M_RDONLY);
    dump_info(fd);
} catch (Exception e) {
    if (e == E_constraint)
        printf "`%s' is not a valid FAT image\n", filename;
    else if (e == E_io)
        printf "Couldn't open file `%s'\n", filename;
    else
        raise e;

    exit(1);
}
